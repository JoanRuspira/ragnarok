//===== Script =======================================
// Innkeeper NPCs
//====================================================


//======================== Prontera ===================================
// West Side Inn -------------------
prt_in,244,135,2	script	Inn Employee#Sammy	53,{
	callfunc "F_InnMaid","[Employee Sammy]","Nenkaras","prt_in",238,130;
	warp "prt_in",247,104; end;
}
// East Side Inn ------------------
prt_in,61,141,2	script	Inn Employee#Ahlma	53,{
	callfunc "F_InnMaid","[Employee Ahlma]","Nenkaras","prt_in",64,136;
	warp "prt_in",60,166; end;
}

//======================== Rachel ====================================
ra_in01,376,69,4	script	Inn Keeper#rachel	931,{
	callfunc "F_InnMaid","[Inn Keeper]","'The Central Inn'","ra_in01",376,64;
	warp "ra_in01",345,130; end;
}

ra_in01,241,195,3	script	Inn Keeper#rachel2	959,{
	callfunc "F_InnMaid","[Inn Keeper]","'The Resting Adventurer'","ra_in01",237,194;
	warp "ra_in01",294,194; end;
}

//======================== Yuno ====================================
yuno_in01,36,179,4	script	Inn Keeper#yuno	46,{
	callfunc "F_InnMaid","[Inn Keeper]","'The Three Cups Tavern and Inn'","yuno_in01",36,174;
	warp "yuno_in01",99,169; end;
}

//====================== Ayothaya ===================================
ayo_in01,31,95,3	script	Inn Keeper#Ayothaya	61,{
	callfunc "F_InnMaid","[Inn Keeper]","'The Ancient Snake Inn'","ayo_in01",26,96;
	warp "ayo_in01",20,22; end;
}

//====================== Amatsu ===================================
ama_in01,162,17,2	script	Inn Keeper#Amatsu	760,{
	callfunc "F_InnMaid","[Inn Keeper]","'The Amatsu Tavern & Inn'","ama_in01",160,22;
	warp "ama_in01",178,74; end;
}

//====================== Louyang North ===================================
lou_in02,55,184,2	script	Inn Keeper#Louyang1	53,{
	callfunc "F_InnMaid","[Inn Keeper]","'Louyang Tavern & Inn'","lou_in02",55,179;
	warp "lou_in02",42,128; end;
}

//====================== Louyang South ===================================
lou_in02,254,56,3	script	Inn Keeper#Louyang2	760,{
	callfunc "F_InnMaid","[Inn Keeper]","'The Secret Dragon Inn'","lou_in02",249,57;
	warp "lou_in02",266,69; end;
}


//====================== TidalIsland ===================================
nameless_n,250,130,3	script	Inn Keeper#Tidal	53,{
	callfunc "F_InnMaid","[Inn Keeper]","'The Tidal Inn'","nameless_n",247,127;
	warp "nameless_n",247,127; end;
}

//====================== Veins ===================================
ve_in,94,231,4	script	Inn Keeper#Veins	798,{
	callfunc "F_InnMaid","[Inn Keeper]","'Thor's Inn'","ve_in",93,225;
	warp "ve_in",110,160; end;
}

//====================== Nifflheim ===================================
nif_in,35,27,3	script	Inn Keeper#Nifflheim	46,{
	callfunc "F_InnMaid","[Inn Keeper]","'The Midnight Crescent' Inn","nif_in",31,27;
	warp "nif_in",67,16; end;
}

//====================== Hel Camp ===================================
mid_campin,93,123,4	script	Inn Keeper#Hel	61,{
	callfunc "F_InnMaid","[Inn Keeper]","'The Tent' Inn","dic_in01",97,121;
	warp "dic_in01",67,184; end;
}

//====================== Dicastes ===================================
dic_in01,377,278,5	script	Inn Keeper#Dicastes	53,{
	callfunc "F_InnMaid","[Inn Keeper]","'The Chill Factory' Inn","dic_in01",375,273;
	warp "dic_in01",358,282; end;
}

//======================== Izlude ====================================
izlude_in,116,76,5	script	Inn Keeper#Izlude	798,{
	callfunc "F_InnMaid","[Inn Keeper]","'The Tumbing Dantini'","izlude_in",111,73;
	warp "izlude_in",111,73; end;
}

//======================== Alberta ====================================
//North
alberta_in,32,141,2	script	Inn Keeper#Alberta	46,{
	callfunc "F_InnMaid","[Inn Keeper]","'The Fisherman'!","alberta_in",26,142;
	warp "alberta_in",18,188; end;
}

//South
alberta_in,55,142,4	script	Inn Keeper#Alberta2	61,{
	callfunc "F_InnMaid","[Inn Keeper]","'The Southport Inn'!","alberta_in",61,142;
	warp "alberta_in",62,103; end;
}

//====================== Geffen ======================================
geffen_in,70,65,7	script	Inn Keeper#Geffen	53,{
	callfunc "F_InnMaid","[Inn Keeper]","'The Ifrit Inn', the only Inn in the city of Geffen.","geffen_in",70,59;
	warp "geffen_in",31,31; end;
}

//======================= Payon ======================================
payon,219,122,4	script	Inn Keeper#Payon	61,{
	callfunc "F_InnMaid","[Inn Keeper]","'The Payon Tavern & Inn'","payon",215,120;
	warp "payon_in01",215,120; end;
}

//======================= Archer ======================================
payon_in02,47,12,5	script	Inn Keeper#Archer	53,{
	callfunc "F_InnMaid","[Inn Keeper]","'The Archer's Rest'","payon_in02",47,9;
	warp "payon_in01",55,12; end;
}

//======================== Morocc ====================================
// North
morocc_in,148,138,2	script	Inn Keeper#Morroc	798,{

	callfunc "F_InnMaid","[Inn Keeper]","'The Star Crumb Inn'","morocc_in",142,140;
	warp "morocc_in",173,135; end;
}
// South
morocc_in,80,101,4	script	Inn Keeper#Morroc2	61,{

	callfunc "F_InnMaid","[Inn Keeper]","'The Nimble Zerom'","morocc_in",78,95;
	warp "morocc_in",79,123; end;
}

//====================== Aldebaran ===================================
aldeba_in,225,114,2	script	Inn Keeper#Aldebaran	53,{
	callfunc "F_InnMaid","[Inn Keeper]","'The Old Duke' Inn","aldeba_in",221,114;
	warp "aldeba_in",214,61; end;
}

//====================== Comodo ===================================
cmd_in02,141,183,4	script	Inn Keeper#Comodo	115,{
	callfunc "F_InnMaid","[Inn Keeper]","'The Kafra West' Inn","cmd_in02",148,180;
	warp "cmd_in02",160,206; end;
}

//====================== Umbala ===================================
um_in,155,70,7	script	Inn Keeper#Umbala	798,{
	callfunc "F_InnMaid","[Inn Keeper]","'Yggdrassil's Inn'","um_in",150,70;
	warp "um_in",144,51; end;
}

//======================== Hugel ====================================
hu_in01,245,108,2	script	Inn Keeper#Hugel	46,{
	callfunc "F_InnMaid","[Inn Keeper]","'The Dragon's Den' Inn","hu_in01",240,109;
	warp "hu_in01",246,93; end;
}

//======================== Einbech ====================================
ein_in01,280,86,3	script	Inn Keeper#Einbech	61,{
	callfunc "F_InnMaid","[Inn Keeper]","'The Einbech Tavern & Inn'","ein_in01",275,84;
	warp "ein_in01",275,84; end;
}

//====================== Lighthalzen =================================
lhz_in02,231,284,4	script	Hotel Employee#lhz	798,{
	mes "[Hotel Employee]";
	mes "Welcome to";
	mes "the Imperial Hotel,";
	mes "where you can find the";
	mes "finest accomodations";
	mes "and the best service.";
	next;
	switch(select("Save Point:Rest - 5,000 zeny:Cancel")) {
	case 1:
		savepoint "lhz_in02",209,275,1,1;
		mes "[Hotel Employee]";
		mes "Thank you, your";
		mes "Respawn Point has";
		mes "been saved here in";
		mes "the Royal Dragon.";
		close;
	case 2:
		if(Zeny < 5000) {
			mes "[Hotel Employee]";
			mes "I'm sorry, but";
			mes "you need 5,000 zeny";
			mes "in order to check in.";
			close;
		}
		set Zeny,Zeny - 5000;
		mes "[Hotel Employee]";
		mes "Thank you~";
		mes "I hope you enjoy";
		mes "your stay in the";
		mes "Royal Dragon.";
		close2;
		warp "lhz_in02",219,150;
		percentheal 100,100;
		npcskill "AL_BLESSING",10,99,99;
		end;
	case 3:
		mes "[Hotel Employee]";
		mes "Thank you and";
		mes "have a nice day.";
		close;
	}
}

//====================== Einbroch =================================
ein_in01,207,224,3	script	Hotel Employee#ein	798,{
	mes "[Hotel Employee]";
	mes "Good day, welcome to the";
	mes "Einbroch Hotel. The staff is";
	mes "always striving to accommodate";
	mes "our guests with the highest";
	mes "standards in cleanliness,";
	mes "service and convenience~";
	next;
	if (kain_ticket == 5) {
		mes "[Hotel Employee]";
		mes "Are you looking";
		mes "for Mr. Defru Ark?";
		mes "Oh right, he did mention";
		mes "waiting for some package";
		mes "from the Airport. Now let";
		mes "me pull up that information...";
		next;
		mes "[Hotel Employee]";
		mes "Ah, here we are.";
		mes "Mr. Defru Ark is";
		mes "staying in Room 201.";
		next;
	}
	else {
		switch(select("Save:Take Rest - 5,000 zeny:Cancel")) {
		case 1:
			mes "[Hotel Employee]";
			mes "Your Respawn Point";
			mes "has been saved here";
			mes "in the Einbroch Hotel.";
			mes "Thank you, and please";
			mes "come again.";
			close2;
			savepoint "ein_in01",200,224,1,1;
			end;
		case 2:
			if (Zeny > 4999) {
				mes "[Hotel Employee]";
				mes "Thank you.";
				mes "Please enjoy";
				mes "your rest~";
				close2;
				set Zeny, Zeny-5000;
				percentheal 100,100;
				warp "ein_in01",272,167;
				end;
			}
			else {
				mes "[Hotel Employee]";
				mes "I'm sorry, but the";
				mes "accommodation fee is";
				mes "5,000 zeny. Next time,";
				mes "please make sure that you";
				mes "bring enough zeny, okay?";
				close;
			}
		case 3:
			mes "[Hotel Employee]";
			mes "Thank you and";
			mes "please come again~";
			close;
		}
	}
	mes "[Hotel Employee]";
	mes "Have a good day.";
	close;
}

//======================= Inn Function ==============================
function	script	F_InnMaid	{
	.@npc_name$ = getarg(0);

	mes .@npc_name$;
	mes "Welcome to the";
	mes getarg(1) + ".";
	mes "How may I help you?";
	next;
	switch(select("Save:Take a Rest -> 5000 zeny:Cancel")) {
	case 1:
		mes .@npc_name$;
		mes "Your respawn point";
		mes "has been saved.";
		mes "Thank you,";
		mes "please come again.";
		savepoint getarg(2),getarg(3),getarg(4),1,1;
		close;
	case 2:
		mes .@npc_name$;
		if(Zeny < 5000){
			mes "I'm sorry, but the service charge is 5,000 zeny. Please make sure that you have enough money to check in next time, okay?";
			close;
		}
		mes "Thank you.";
		mes "I hope you";
		mes "enjoy your rest~";
		close2;
		set Zeny,Zeny - 5000;
		percentheal 100,100;
		return;
	case 3:
		close;
	}
}
