

-	script	Dye Maker#dyemker::dyemker	-1,{
	//- multi dyestuff 0/1 - disabled/enabled
	.multi = 1;
	//-
	
	if ((MaxWeight-Weight) < 200 || checkweight(1201,1) == 0) {
		mes "- Wait a moment! -";
		mes "- Currently you're carrying -";
		mes "- too many items with you. -";
		mes "- Please come back later -";
		mes "- after you put some items into kafra storage. -";
		close;
	}
	mes "[Dye Maker Java Dullihan]";
	mes "Wow...";
	mes "Such a nice day. Days like this are perfect to make dyes.";
	next;
	switch(select("Talk:Make Dyestuffs:Cancel")) {
	case 1:
		mes "[Dye Maker Java Dullihan]";
		mes "Erm, I don't really have much to say to you. But, if you would like me to tell you about my life, I can do that. It's a little long and boring, but would you like to listen?";
		next;
		if (select("Listen:Don't Listen") == 1) {
			mes "[Dye Maker Java Dullihan]";
			mes "As long as I can remember, my father has been making dyes. He used to spend countless hours making dyes of different colors. Even when my mother passed away, he never stopped.";
			next;
			mes "[Dye Maker Java Dullihan]";
			mes "At one point in my life, I became rebellious and ran away from home. I didn't want to be stuck to the family business and wanted to try other things.";
			next;
			mes "[Dye Maker Java Dullihan]";
			mes "Anyways...";
			mes "I eventually wound up back home to carry on the family tradition, making dyes for 15 years already. I guess it was really in my blood.";
			next;
			mes "[Dye Maker Java Dullihan]";
			mes "Something I realized these days is that now I can understand my father. Why my father devoted everything to making dyes...";
			next;
			mes "[Dye Maker Java Dullihan]";
			mes "In the middle of all that tedious and hard work, he probably felt the magic of those colors passing on so many dreams for other people.";
			next;
			mes "[Dye Maker Java Dullihan]";
			mes "It was probably for those dreams that he tried so hard to make dyes for his customers...";
			next;
			mes "[Dye Maker Java Dullihan]";
			mes "Heh... How do I know this? Well, that's the way I feel right now. Since a couple years ago, I've been able to hear what the colors were saying...";
			next;
			mes "[Dye Maker Java Dullian]";
			mes "Just watch. Someday, I'm going to succeed in making the color my father wanted to, but never got the chance to make.";
			close;
		}
		mes "[Dye Maker Java Dullihan]";
		mes "Ahahahaha...";
		mes "Well, I guess no one would want to listen to a measly dyemaker's story anyway. Hahaha....";
		close;
	
	case 2:
		mes "[Dye Maker Java Dullihan]";
		mes "Great! If you want it, I'll make it for you. I promise I'll make you the color you want. But what color dye would you like to make? The fee is different depending on the color.";
		next;
		mes "[Dye Maker Java Dullihan]";
		mes "Why, do you ask? Just remember that there are colors that are easy to make, and those that require more effort and work. Please don't think the fee is too expensive.";
		next;
		mes "[Dye Maker Java Dullihan]";
		mes "The color of the dye is special, made with all my heart and soul.";
		next;
		switch( select( .@a$ = "Scarlet Dyestuffs:Lemon Dyestuffs:Cobaltblue Dyestuffs:Darkgreen Dyestuffs:Orange Dyestuffs:Violet Dyestuffs:White Dyestuffs:Black Dyestuffs:Cancel" )) {
		case 1: callsub S_MakeDye,1,1,.@a$; break;
		case 2: callsub S_MakeDye,2,1,.@a$; break;
		case 3: callsub S_MakeDye,3,2,.@a$; break;
		case 4: callsub S_MakeDye,4,3,.@a$; break;
		case 5: callsub S_MakeDye,5,4,.@a$; break;
		case 6: callsub S_MakeDye,6,4,.@a$; break;
		case 7: callsub S_MakeDye,7,4,.@a$; break;
		case 8: callsub S_MakeDye,8,4,.@a$; break;
		case 9:
			mes "[Dye Maker Java Dullihan]";
			mes "Eeeehhhh!! What's this? You change your mind now!? So disappointing...";
			close;
		}
	
	case 3:
		mes "[Dye Maker Java Dullihan]";
		mes "I'm not bragging or anything. But I have the skills to make dyestuff. If you ever need dyestuff, please come to me. I'll make them for you at a reasonable price.";
		close;
	}
	
	S_MakeDye:
	mes "[Dye Maker Java Dullihan]";
	   if ( .multi ) {
		   explode(.@b$, getarg(2), ":");
		   mes "How must ^FF0000"+ .@b$[ @menu-1 ] +"^000000 do you want to do ?";
		   if( input( .@n, 1, 100000) != 0 ) {
			   mes "Sorry, wrong value.";
			   close; }
	   } else
		   .@n = 1;
	switch(getarg(0)) {
	case 1:
		setarray .@count[0], 30*.@n,   1*.@n,   1*.@n;
		set .@cost,3000*.@n;
		mes "Mmm... I need "+.@count[0]+" Red Herbs, "+.@count[1]+" Counteragent, and "+.@count[2]+" Empty Bottle to make Red Dyestuffs. The fee is only "+.@cost+" zeny to make it.";
		setarray .@item[0], 507, 973, 713;
		set .@dyestuff,975;
		break;
	case 2:
		setarray .@count[0], 30*.@n,   1*.@n,   1*.@n;
		set .@cost,3000*.@n;
		mes "Mmm... I need "+.@count[0]+" Yellow Herbs, "+.@count[1]+" Couneragent, and "+.@count[2]+" Empty Bottle to make Lemon Dyestuffs. The fee is only "+.@cost+" zeny to make it.";
		setarray .@item[0], 508, 973, 713;
		set .@dyestuff,976;
		break;
	case 3:
		setarray .@count[0], 20*.@n,   1*.@n,   1*.@n;
		set .@cost,3500*.@n;
		mes "Mmm... I need "+.@count[0]+" Blue Herbs, "+.@count[1]+" Counteragent, and "+.@count[2]+" Empty Bottle to make Cobaltblue Dyestuff. It's hard to use the Blue Herb, so the fee is going to be "+.@cost+" zeny.";
		setarray .@item[0], 510, 973, 713;
		set .@dyestuff,978;
		break;
	case 4:
		setarray .@count[0],  5*.@n,  20*.@n,  20*.@n,   1*.@n,   1*.@n,   1*.@n;
		set .@cost,5000*.@n;
		mes "Mmm... I need "+.@count[0]+" Blue Herbs, "+.@count[1]+" Green Herbs, "+.@count[2]+" Yellow Herbs, "+.@count[3]+" Counteragent, "+.@count[4]+" Mixture, and "+.@count[5]+" Empty Bottle to make Darkgreen Dyestuffs. Don't get all of the materials confused. The fee is only "+.@cost+" zeny.";
		setarray .@item[0], 510, 511, 508, 974, 973, 713;
		set .@dyestuff,979;
		break;
	case 5:
		setarray .@count[0], 20*.@n,  20*.@n,   1*.@n,   1*.@n,   1*.@n;
		set .@cost,5000*.@n;
		mes "Mmm... I need "+.@count[0]+" Red Herbs, "+.@count[1]+" Yellow Herbs, "+.@count[2]+" Counteragent, "+.@count[3]+" Mixture, and "+.@count[4]+" Empty Bottle to make Orange Dyestuff. The fee is going to be "+.@cost+" zeny.";
		setarray .@item[0], 507, 508, 974, 973, 713;
		set .@dyestuff,980;
		break;
	case 6:
		setarray .@count[0], 10*.@n,  30*.@n,   1*.@n,   1*.@n,   1*.@n;
		set .@cost,5000*.@n;
		mes "Mmm... I need "+.@count[0]+" Blue Herbs, "+.@count[1]+" Red Herbs, "+.@count[2]+" Counteragent, "+.@count[3]+" Mixture, and "+.@count[4]+" Empty Bottle to make Violet Dyestuffs. The fee will be "+.@cost+" zeny.";
		setarray .@item[0], 510, 507, 974, 973, 713;
		set .@dyestuff,981;
		break;
	case 7:
		setarray .@count[0], 30*.@n,   1*.@n,   1*.@n;
		set .@cost,3000*.@n;
		mes "Mmm... I need "+.@count[0]+" White Herbs, "+.@count[1]+" Counteragent, and "+.@count[2]+" Empty bottle to make White Dyestuffs. The fee will be "+.@cost+" zeny.";
		setarray .@item[0], 509, 973, 713;
		set .@dyestuff,982;
		break;
	case 8:
		setarray .@count[0], 30*.@n,  30*.@n,  30*.@n,   5*.@n,   1*.@n,   1*.@n,   1*.@n;
		set .@cost,7000*.@n;
		mes "Mmm... I need "+.@count[0]+" of each Red, Yellow, and Green Herb, "+.@count[3]+" Blue Herbs, "+.@count[4]+" Counteragent, "+.@count[5]+" Mixture, and "+.@count[6]+" Empty Bottle. The process takes longer and more effort than the others, so it is going to be "+.@cost+" zeny.";
		setarray .@item[0], 507, 508, 511, 510, 974, 973, 713;
		set .@dyestuff,983;
		break;
	}
	next;
	mes "[Dye Maker Java Dullihan]";
	switch(getarg(1)) {
	case 1: mes "Ah! I think you would have everything ready. Would you like to start the process?"; break;
	case 2: mes "Okay! I believe you would have everything ready. Would you like to start the process?"; break;
	case 3: mes "Okay! I believe you would have everything ready. Shall we begin the process?"; break;
	case 4: mes "Okay! I believe you would have everything prepared. Would you like to start the process?"; break;
	}
	next;
	if (select("Make Dyestuffs:Cancel") == 1) {
		set .@size, getarraysize(.@item);
		// check requirements
		for( set .@i,0 ; .@i < .@size ; set .@i,.@i+1 )
			if( countitem(.@item[.@i]) < .@count[.@i] )
				break;
		if( .@i < .@size || Zeny < .@cost ) {
			mes "[Dye Maker Java Dullihan]";
			mes "Hmmm. Not enough...";
			mes "I don't think I'll be able to make the color you want with those materials. Why don't you go get some more materials...?";
			close;
		}
		// delete items
		for (set .@i, 0; .@i < .@size ; set .@i, .@i+1)
			delitem .@item[.@i], .@count[.@i];
		set zeny,zeny-.@cost;
		// get dyestuff
		getitem .@dyestuff,.@n;
	
		mes "[Dye Maker Java Dullihan]";
		mes "Hmm... It came out pretty well. A very rich color. Of course I'll be trying harder to make a more charming color...";
		next;
		mes "[Dye Maker Java Dullihan]";
		mes "Well, then. Stop by whenever you need more dyes.";
		close;
	}
	mes "[Dye Maker Java Dullihan]";
	mes "Eeeehhhh!! What's this?";
	mes "You change your mind now!? So disappointing...";
	return;
}

prontera,259,282,3	duplicate(dyemker)	Dye Maker#dyemker	59