//===== Script =======================================
// Refining Related NPCs
//====================================================

// Material Salesmen
//============================================================
lhz_in02,282,19,3	script	Material Merchant#matmch1	86,{
	callfunc "phramain","Material Merchant";
	end;
}
ein_in01,15,87,3	script	Material Merchant#matmch2	86,{
	callfunc "phramain","Material Merchant";
	end;
}
izlude,154,130,2	script	Material Merchant#matmch3	85,{
	callfunc "phramain","Material Merchant ";
	end;
}
payon,147,172,4	script	Material Merchant#matmch4	88,{
	callfunc "phramain","Material Merchant";
	end;
}
alberta_in,15,65,5	script	Material Merchant#matmch5	10,{
	callfunc "phramain","Material Merchant";
	end;
}
morocc_in,63,36,0	script	Material Merchant#matmch6	726,{
	callfunc "phramain","Material Merchant";
	end;
}
mid_camp,153,269,7	script	Material Merchant#matmch7	726,{
	callfunc "phramain","Material Merchant";
	end;
}
lou_in01,25,23,5	script	Material Merchant#matmch10	726,{
	callfunc "phramain","Material Merchant";
	end;
}
rachel,195,49,0	script	Material Merchant#matmch11	726,{
	callfunc "phramain","Material Merchant";
	end;
}
prt_in,56,68,5	script	Material Merchant#matmch8	86,{
	callfunc "phramain","Material Merchant";
	end;
}
yuno_in01,164,23,7	script	Material Merchant#matmch9	88,{
	callfunc "phramain","Material Merchant";
	end;
}




// Ori-Elu Refiners
//============================================================

lighthalzen,51,82,4	script	Material Polisher#matpol1	84,{
	callfunc "orimain","Material Polisher";
	end;
}
ein_in01,18,82,6	script	Material Polisher#matpol2	84,{
	callfunc "orimain","Material Polisher";
	end;
}
izlude,147,129,5	script	MaterialPolisher#matpol3	88,{
	callfunc "orimain","Material Polisher";
	end;
}
payon,142,181,7	script	Material Polisher#matpol4	85,{
	callfunc "orimain","Material Polisher";
	end;
}
alberta_in,21,63,5	script	Material Polisher#matpol5	726,{
	callfunc "orimain","Material Polisher";
	end;
}
morocc_in,72,32,3	script	Material Polisher#matpol6	63,{
	callfunc "orimain","Material Polisher";
	end;
}
mid_camp,152,253,0	script	Material Polisher#matpol7	63,{
	callfunc "orimain","Material Polisher";
	end;
}
ve_in02,43,22,1	script	Material Polisher#matpol8	85,{
	callfunc "orimain","Material Polisher";
	end;
}
ama_in01,18,102,0	script	Material Polisher#matpol9	85,{
	callfunc "orimain","Material Polisher";
	end;
}
prt_in,63,69,3	script	Material Polisher#matpol00	84,{
	callfunc "orimain","Material Polisher";
	end;
}
yuno_in01,166,31,5	script	Material Polisher#matpol000	10,{
	callfunc "orimain","Material Polisher";
	end;
}



// Equipment Repairmen
//============================================================
dic_in01,208,266,7	script	Repairman#rpmn1	813,{
	callfunc "repairmain","Repairman";
	end;
}
lighthalzen,52,79,3	script	Repairman#rpmn2	63,{
	callfunc "repairmain","Repairman";
	end;
}
ein_in01,31,89,4	script	Repairman#rpmn3	63,{
	callfunc "repairmain","Repairman";
	end;
}
einbech,153,249,4	script	Repairman#rpmn4	63,{
	callfunc "repairmain","Repairman";
	end;
}
hugel,149,153,4	script	Repairman#rpmn5	63,{
	callfunc "repairmain","Repairman";
	end;
}
geffen_in,144,171,3	script	Repairman#rpmn6	99,{
	callfunc "repairmain","Repairman";
	end;
}
izlude,150,122,4	script	Repairman#rpmn7	813,{
	callfunc "repairmain","Repairman";
	end;
}
payon,139,173,4	script	Repairman#rpmn8	731,{
	callfunc "repairmain","Repairman";
	end;
}
aldeba_in,38,60,3	script	Repairman#rpmn9	86,{
	callfunc "repairmain","Repairman";
	end;
}
alberta_in,31,65,4	script	Repairman#rpmn10	86,{
	callfunc "repairmain","Repairman";
	end;
}
morocc_in,65,31,1	script	Repairman#rpmn11	826,{
	callfunc "repairmain","Repairman";
	end;
}
comodo,227,171,3	script	Repairman#rpmn12	731,{
	callfunc "repairmain","Repairman";
	end;
}
cmd_fild07,112,186,3	script	Repairman#rpmn13	10,{
	callfunc "repairmain","Repairman";
	end;
}
mid_camp,152,240,3	script	Repairman#rpmn14	10,{
	callfunc "repairmain","Repairman";
	end;
}
ve_in02,56,16,1	script	Repairman#rpmn15	63,{
	callfunc "repairmain","Repairman";
	end;
}
nameless_n,84,260,4	script	Repairman#rpmn16	826,{
	callfunc "repairmain","Repairman";
	end;
}
ama_in01,17,109,7	script	Repairman#rpmn17	826,{
	callfunc "repairmain","Repairman";
	end;
}
ayo_in01,184,171,1	script	Repairman#rpmn18	731,{
	callfunc "repairmain","Repairman";
	end;
}
lou_in01,16,26,5	script	Repairman#rpmn19	10,{
	callfunc "repairmain","Repairman";
	end;
}
rachel,191,77,5	script	Repairman#rpmn20	731,{
	callfunc "repairmain","Repairman";
	end;
}
prt_in,63,54,2	script	Repairman#rpmn00	86,{
	callfunc "repairmain","Repairman";
	end;
}
yuno_in01,162,27,6	script	Repairman#rpmn000	731,{
	callfunc "repairmain","Repairman";
	end;
}



// Weapon/Armor Refiners
//============================================================
dic_in01,214,266,3	script	Refiner#rfnr1	726,{
	callfunc "refinemain","Refiner",0;
	end;
}
lighthalzen,55,81,3	script	Refiner#rfnr2	726,{
	callfunc "refinemain","Refiner",0;
	end;
}
ein_in01,24,87,5	script	Refiner#rfnr3	826,{
	callfunc "refinemain","Refiner",0;
	end;
}
einbech,150,261,5	script	Refiner#rfnr4	726,{
	callfunc "refinemain","Refiner",0;
	end;
}
hugel,161,153,3	script	Refiner#rfnr5	813,{
	callfunc "refinemain","Refiner",0;
	end;
}
aldebaran,69,96,3	script	Refiner#rfnr6	826,{
	callfunc "refinemain","Refiner",0;
	end;
}
geffen_in,103,162,1	script	Refiner#rfnr7	726,{
	callfunc "refinemain","Refiner",0;
	end;
}
izlude,154,119,3	script	Refiner#rfnr8	63,{
	callfunc "refinemain","Refiner",0;
	end;
}
payon,141,164,5	script	Refiner#rfnr9	726,{
	callfunc "refinemain","Refiner",0;
	end;
}
alberta_in,28,58,0	script	Refiner#rfnr10	63,{
	callfunc "refinemain","Refiner",0;
	end;
}
morocc_in,73,38,4	script	Refiner#rfnr11	731,{
	callfunc "refinemain","Refiner",0;
	end;
}
comodo,226,166,0	script	Refiner#rfnr12	63,{
	callfunc "refinemain","Refiner",0;
	end;
}
cmd_fild07,107,189,2	script	Refiner#rfnr13	726,{
	callfunc "refinemain","Refiner",0;
	end;
}
umbala,171,143,7	script	Refiner#rfnr14	826,{
	callfunc "refinemain","Refiner",0;
	end;
}
mid_camp,163,240,5	script	Refiner#rfnr15	826,{
	callfunc "refinemain","Refiner",0;
	end;
}
ve_in02,62,25,5	script	Refiner#rfnr16	726,{
	callfunc "refinemain","Refiner",0;
	end;
}
niflheim,321,64,5	script	Refiner#rfnr17	63,{
	callfunc "refinemain","Refiner",0;
	end;
}
ama_in01,26,105,0	script	Refiner#rfnr18	63,{
	callfunc "refinemain","Refiner",0;
	end;
}
ayo_in01,180,192,5	script	Refiner#rfnr19	726,{
	callfunc "refinemain","Refiner",0;
	end;
}
lou_in01,15,14,6	script	Refiner#rfnr20	63,{
	callfunc "refinemain","Refiner",0;
	end;
}
rachel,194,79,4	script	Refiner#rfnr21	63,{
	callfunc "refinemain","Refiner",0;
	end;
}
prt_in,63,60,0	script	#rfnrRefiner00	85,{
	callfunc "refinemain","Refiner",0;
	end;
}
yuno_in01,173,25,2	script	Refiner#rfnr000	726,{
	callfunc "refinemain","Refiner",0;
	end;
}




//============================================================
//= Main Refiner Function
//============================================================
function	script	refinemain	{
	disable_items;
	.@npc_name$ = getarg(0);
	.@features = getarg(1);
	mes "["+ .@npc_name$ +"]";
	mes "I'm the Armsmith.";
	mes "I can refine all kinds of weapons, armor and equipment, so let me";
	mes "know what you want me to refine.";
	next;

	setarray .@indices[1], EQI_HEAD_TOP, EQI_ARMOR, EQI_HAND_L, EQI_HAND_R, EQI_GARMENT, EQI_SHOES, EQI_ACC_L, EQI_ACC_R, EQI_HEAD_MID, EQI_HEAD_LOW;
	for(.@i = 1; .@i<getarraysize(.@indices); ++.@i) {
		if(getequipisequiped(.@indices[.@i])) {
			.@menu$ = .@menu$ + F_getpositionname(.@indices[.@i]) + "-[" + getequipname(.@indices[.@i]) + "]";
			.@equipped = 1;
		}
		.@menu$ = .@menu$ + ":";
	}
	if (.@equipped == 0) {
		mes "["+ .@npc_name$ +"]";
		mes "I don't think I can refine any items you have...";
		close;
	}
	.@part = .@indices[select(.@menu$)];

	if(!getequipisequiped(.@part)) { //custom check
		mes "["+ .@npc_name$ +"]";
		mes "You're not wearing";
		mes "anything there that";
		mes "I can refine.";
		emotion ET_FRET;
		close;
	}
	//Check if the item is refinable...
	if(!getequipisenableref(.@part)) {
		mes "["+ .@npc_name$ +"]";
		mes "I don't think I can";
		mes "refine this item at all...";
		close;
	}
	//Check to see if the items is already +10
	if(getequiprefinerycnt(.@part) >= 10) {
		mes "["+ .@npc_name$ +"]";
		mes "I can't refine this";
		mes "any more. This is as";
		mes "refined as it gets!";
		close;
	}
	.@refineitemid = getequipid(.@part); // save id of the item
	.@refinerycnt = getequiprefinerycnt(.@part); //save refinery count
	setarray .@card[0], getequipcardid(.@part,0), getequipcardid(.@part,1), getequipcardid(.@part,2), getequipcardid(.@part,3);
	.@price = getequiprefinecost(.@part, REFINE_COST_NORMAL, REFINE_ZENY_COST);
	.@material = getequiprefinecost(.@part, REFINE_COST_NORMAL, REFINE_MATERIAL_ID);

	switch (getequipweaponlv(.@part)) {
		case 1: .@safe = 7; break;
		case 2: .@safe = 6; break;
		case 3: .@safe = 5; break;
		case 4:
		default: .@safe = 4; break;
	}

	// If the VIP system is enabled, the prices for non-VIP players are considerably higher.
	if (VIP_SCRIPT && !vip_status(VIP_STATUS_ACTIVE)) {
		switch(getequipweaponlv(.@part)) {
			case 0: .@price = .@price * 10; break;
			case 1: .@price = .@price * 40; break;
			case 2: .@price = .@price * 50; break;
			case 3: .@price = .@price * 2; break;
			case 4: .@price = .@price * 2; break;
			case 5: .@price = .@price * 10; break;
		}
	}

	if(.@features != 1) {
		mes "["+ .@npc_name$ +"]";
		mes "To refine this I need";
		mes "one ^003366"+getitemname(.@material)+"^000000 and";
		mes "a service fee of " + .@price + " Zeny.";
		mes "Do you really wish to continue?";
		next;
		if(select("Yes:No") == 2){
			mes "["+ .@npc_name$ +"]";
			mes "Yeah...";
			mes "There's no need to";
			mes "rush. Take your time.";
			close;
		}
		if(getequippercentrefinery(.@part) < 100) {
			mes "["+ .@npc_name$ +"]";
			mes "Oh no! If I continue to";
			mes "refine this, there's a risk it could";
			switch(.@material) {
			case 985:
				mes "be destroyed! That means that ^FF0000this equipment^000000, and ^FF0000any cards^000000 or special properties added to this armor, ^FF0000will be gone^000000.";
				break;
			default:
				mes "be destroyed, and you'd ^FF0000lose the weapon^000000, any ^FF0000cards in the weapon^000000,";
				mes "or any added special properties.";
				break;
			}
			next;
			mes "["+getarg(0)+"]";
			mes "I can't make it any clearer.";
			mes "Once a weapon is destroyed,";
			mes "there's no getting it back.";
			mes "You really have a chance to";
			mes "^FF0000lose this weapon^000000 forever.";
			mes "Do you still want to refine?";
			next;
			if(select("Yes:No") == 2){
				mes "["+ .@npc_name$ +"]";
				mes "I completely agree...";
				mes "I might be a great refiner, but sometimes even I make mistakes.";
				close;
			}
		}
		if((countitem(.@material) < 1) || (Zeny < .@price)) {
			mes "["+ .@npc_name$ +"]";
			mes "You don't seem to have";
			mes "enough Zeny or "+getitemname(.@material)+"...";
			mes "Go get some more. I'll be";
			mes "here all day if you need me.";
			close;
		}
		Zeny = Zeny-.@price;
		delitem .@material,1;

		// anti-hack
		if (callfunc("F_IsEquipIDHack", .@part, .@refineitemid) || callfunc("F_IsEquipCardHack", .@part, .@card[0], .@card[1], .@card[2], .@card[3]) ||
		    callfunc("F_IsEquipRefineHack", .@part, .@refinerycnt)) {
			mes "["+ .@npc_name$ +"]";
			emotion ET_FRET;
			mes "Wait a second...";
			mes "Do you think I'm stupid?!";
			mes "You switched the item while I wasn't looking! Get out of here!";
			close;
		}

		if(getequippercentrefinery(.@part) <= rand(100)) {
			failedrefitem .@part;
			mes "["+ .@npc_name$ +"]";
			emotion (!rand(5))?ET_MONEY:ET_HUK;
			.@lose = rand(1,3);
			if (.@lose == 1) {
				mes "OH! MY GOD!";
				mes "Damn it! Not again!";
				mes "I'm terribly sorry, but you know practice does make perfect.";
				mes "Um, right? Heh heh...";
			} else if(.@lose == 2) {
				mes "Nooooooo!";
				mes "It broke!";
				mes "I-I'm sorry!";
			} else {
				mes "Crap!";
				mes "It couldn't take";
				mes "much more tempering!";
				mes "Sorry about this...";
			}
			close;
		}
		mes "["+getarg(0)+"]";
		successrefitem .@part;
		emotion ET_SMILE;
		.@win = rand(1,3);
		if (.@win == 1) {
			mes "Perfect!";
			mes "Heh heh!";
			mes "Once again,";
			mes "flawless work";
			mes "from the master~";
		} else if(.@win == 2) {
			mes "Success...!";
			mes "Yet again, my amazing";
			mes "talent truly dazzles";
			mes "and shines today.";
		} else {
			mes "Heh heh!";
			mes "I'm all done.";
			mes "No doubt, my work is";
			mes "to your satisfaction.";
			mes "Sheer, utter perfection~";
		}
		close;
	}

// New Refining Functions ========================
	if (.@refinerycnt < .@safe) {
		mes "["+ .@npc_name$ +"]";
		mes "I can refine this to the safe limit or a desired number of times. It's your choice.";
		next;
		.@menu2 = select("To the safe limit, please.","I'll decide how many times.","I've changed my mind...");
	} else
		.@menu2 = 2;
	switch(.@menu2){
	case 1: 
		.@refinecnt = .@safe - .@refinerycnt;
		break;
	case 2:
		next;
		mes "["+ .@npc_name$ +"]";
		mes "How many times would you like me to refine your item?";
		next;
		input .@refinecnt;
		.@refinecheck = .@refinecnt + .@refinerycnt;
		if (.@refinecnt < 1 || .@refinecheck > 10) {
			mes "["+ .@npc_name$ +"]";
			mes "I can't refine this item that many times.";
			close;
		}
		if(.@refinecheck > .@safe) {
			.@refinecheck = .@refinecheck - .@safe;
			mes "["+ .@npc_name$ +"]";
			mes "This will try to refine the equipment " + .@refinecheck + " times past the safe limit. Your equipment may be destroyed... is that ok?";
			next;
			if(select("Yes...","No...") == 2){
				mes "["+ .@npc_name$ +"]";
				mes "You said so... So be it.";
				close;
			}
		}
		break;
	case 3:
		next;
		mes "["+ .@npc_name$ +"]";
		mes "You said so... So be it.";
		close;
	}
	.@fullprice = .@price * .@refinecnt;
	mes "["+ .@npc_name$ +"]";
	mes "That will cost you " + .@refinecnt + " " + getitemname(.@material) + " and " + .@fullprice + " Zeny. Is that ok?";
	next;
	if(select("Yes","No...") == 2){
		mes "["+ .@npc_name$ +"]";
		mes "You said so... So be it.";
		close;
	}
	if(countitem(.@material) < .@refinecnt || Zeny < .@fullprice) {
		mes "["+ .@npc_name$ +"]";
		mes "Is that all you got? Unfortunately I can't work for you at a lower price. Try putting yourself in my shoes.";
		close;
	}
	Zeny = Zeny - .@fullprice;
	delitem .@material,.@refinecnt;
	while(.@refinecnt){
		if (getequipisequiped(.@part) == 0) {
			mes "["+ .@npc_name$ +"]";
			mes "Look here... you don't have any items on...";
			close;
		}
		// anti-hack
		if (callfunc("F_IsEquipIDHack", .@part, .@refineitemid) || callfunc("F_IsEquipCardHack", .@part, .@card[0], .@card[1], .@card[2], .@card[3]) ||
				callfunc("F_IsEquipRefineHack", .@part, .@refinerycnt) || (.@menu2 == 1 && getequippercentrefinery(.@part) < 100)) {
			mes "["+ .@npc_name$ +"]";
			mes "Clang... No, but did you imagine I could be so stupid?!";
			mes "You changed it...";
			mes "Get out before I stun you with my Hammer!!";
			close;
		} 
		mes "Clang, clang!!!";
		if(.@menu2 == 2 && getequippercentrefinery(.@part) <= rand(100)) {
			failedrefitem .@part;
			emotion ET_HUK;
			mes "["+ .@npc_name$ +"]";
			mes "WAHHHH!!! I'm so sorry... I warned you this could happen...";
			.@refinecnt = .@refinecnt - 1;
			if(.@refinecnt == 0) close;
			mes "Here's the unused Zeny and materials back...";
			getitem .@material,.@refinecnt;
			.@fullprice = .@refinecnt * .@price;
			Zeny = Zeny + .@fullprice;
			close;
		}
		successrefitem .@part;
		emotion ET_BEST;
		.@refinecnt = .@refinecnt - 1;
		.@refinerycnt = getequiprefinerycnt(.@part);
		next;
	}
	mes "["+ .@npc_name$ +"]";
	mes "All finished... Come again soon.";
	close;
}



// Material Salesmen Functions
//============================================================
function	script	phramain	{
	if (checkweight(1201,1) == 0) {
		mes "- Wait a minute !! -";
		mes "- Currently you're carrying -";
		mes "- too many items with you. -";
		mes "- Please try again -";
		mes "- after you lose some weight. -";
		close;
	}
	.@npc_name$ = getarg(0);
	mes "["+ .@npc_name$ +"]";
	mes "I sell 2 kinds of Metal";
	mes "for tempering weaponry.";
	mes "I have ^007777Phracon^000000 for Level 1";
	mes "Weapons, and ^007777Emveretarcon^000000";
	mes "for Level 2 Weapons.";
	next;
	switch(select("Phracon - 200 Zeny:Emveretarcon - 1000 Zeny:Ask about other Metals")) {
	case 1:
		.@material = 1010;
		.@price = 200;
		break;
	case 2:
		.@material = 1011;
		.@price = 1000;
		break;
	case 3:
		mes "["+ .@npc_name$ +"]";
		mes "Other metals?";
		mes "Well, you'll need special metals to upgrade higher level weapons, or any kind of armor. But you know, Oridecon and Elunium is really";
		mes "hard to just find...";
		close;
	}
	mes "["+ .@npc_name$ +"]";
	mes "So how many do you wish to buy?";
	mes "If you don't want any, please enter the number, '0.'";
	next;
	while(1) {
		input .@input;
		if (.@input == 0) {
			mes "["+ .@npc_name$ +"]";
			mes "The deal has";
			mes "been cancelled.";
			close;
		}
		else if (.@input < 0 || .@input > 500) {
			mes "["+ .@npc_name$ +"]";
			mes "Alright, you can";
			mes "puchase up to 500.";
			mes "No more than that,";
			mes "got it? Good.";
			next;
		}
		else {
			break;
		}
	}
	.@sell = .@input * .@price;
	if (Zeny < .@sell) {
		mes "["+ .@npc_name$ +"]";
		mes "Err...";
		mes "You don't have";
		mes "enough Zeny to buy";
		mes ""+ .@input +" of them.";
		close;
	}
	if (checkweight(.@material,.@input) == 0) {
		mes "["+ .@npc_name$ +"]";
		mes "Hmm...";
		mes "I can't give you anything if you don't have enough room in your inventory. Why don't you put your extra things in Kafra Storage and try again?";
		close;
	}
	getitem .@material,.@input;
	Zeny = Zeny-.@sell;
	mes "["+ .@npc_name$ +"]";
	mes "Here you are!";
	mes "Thank you for";
	mes "your patronage.";
	close;
}


// Ori/Elu Functions
//============================================================
function	script	orimain	{
	if (checkweight(1201,1) == 0) {
		mes "- Wait a minute !! -";
		mes "- Currently you're carrying -";
		mes "- too many items with you. -";
		mes "- Please try again -";
		mes "- after you lose some weight. -";
		close;
	}
	.@npc_name$ = getarg(0);
	mes "["+ .@npc_name$ +"]";
	mes "I can purify your";
	mes "Rough Oridecons or";
	mes "Rough Eluniums. I'll need";
	mes "5 Rough Stones to make";
	mes "1 pure one for you.";
	next;
	switch(select("Make Oridecon:Make Elunium:Ask about Enchanted Stones")) {
	case 1:
		if (countitem(756) > 4) {
			delitem 756,5;  //Oridecon_Stone
			getitem 984,1; // Oridecon
			mes "["+ .@npc_name$ +"]";
			mes "Here's your Oridecon.";
			mes "You're welcome to come";
			mes "back whenever you want.";
			close;
		}
		else {
			mes "["+ .@npc_name$ +"]";
			mes "You're kidding me, right?";
			mes "I just told you that I need 5 Rough Oridecons to make a pure Oridecon.";
			close;
		}
	case 2:
		if (countitem(757) > 4) {
			delitem 757,5;  //Elunium_Stone
			getitem 985,1; // Elunium
			mes "["+ .@npc_name$ +"]";
			mes "Here's your Elunium.";
			mes "You're welcome to come";
			mes "back whenever you want.";
			close;
		}
		else {
			mes "["+ .@npc_name$ +"]";
			mes "You're kidding me, right?";
			mes "I just told you that I need 5 Rough Eluniums to make a pure Elunium.";
			close;
		}
	case 3:
		mes "["+ .@npc_name$ +"]";
		mes "Enchanted Stones...?";
		mes "I've been a stonesmith for 20 years, so I've heard a lot about them. Supposedly, there are";
		mes "four different kinds.";
		next;
		mes "["+ .@npc_name$ +"]";
		mes "Each Enchanted Stone possesses one of the following elemental properties: Earth, Wind, Water and Fire.";
		next;
		mes "["+ .@npc_name$ +"]";
		mes "If someone combines a Enchanted Stone with a weapon while smithing, that weapon will possess the same property as the Stone.";
		next;
		mes "["+ .@npc_name$ +"]";
		mes "Needless to say, you need to have some smithing skill to produce this kind of elemental weapon.";
		close;
	}
}


// Equipment Repair Function
//============================================================
function	script	repairmain	{
	.@repairprice = 5000;
	.@npc_name$ = getarg(0);
	mes "["+ .@npc_name$ +"]";
	mes "Hey there!";
	mes "Do you want me";
	mes "to repair any items?";
	mes "You can count on me";
	mes "for item repairs!";
	next;
	switch(select("Actually, I do have some items...:None at the moment.")) {
	case 1:
		.@checkitem = 1;
		while (1) {
			if (getbrokenid(.@checkitem) == 0) {
				break;
			}
			.@checkitem = .@checkitem+1;
		}
		.@checkitem = .@checkitem-1;
		if (!.@checkitem) {
			mes "["+ .@npc_name$ +"]";
			mes "Oh wow, this is incredible!";
			mes "You must take very good care of your things. None of your items are damaged!";
			next;
			mes "["+ .@npc_name$ +"]";
			mes "If everyone is like you, I'm going to be unemployed!! Haha~!";
			close;
		}
		mes "["+ .@npc_name$ +"]";
		mes "Hmm...";
		mes "Let's see...";
		mes "Out of all your items,";
		mes "" + .@checkitem + " are damaged.";
		mes "Would you like to repair?";
		next;
		.@totalcost = .@repairprice*.@checkitem;
		mes "["+ .@npc_name$ +"]";
		mes "Each repair costs " + .@repairprice + " Zeny. So to repair all your damaged items would cost " + .@totalcost + " Zeny! Would you like to repair the items?";
		next;
		switch(select("Yes:No")) {
		case 1:
			if (Zeny < .@totalcost) {
				mes "["+ .@npc_name$ +"]";
				mes "Whoa whoa...";
				mes "Check your wallet before you receive the repair bill! I can't repair anything because you don't have enough Zeny.";
				close;
			}
			.@checkitem2 = 1;
			while (1) {
				if (getbrokenid(.@checkitem2) == 0) {
					break;
				}
				.@checkitem2 = .@checkitem2+1;
			}
			.@checkitem2 = .@checkitem2-1;
			if (.@checkitem == .@checkitem2) {
				Zeny = Zeny-.@totalcost;
				while (.@checkitem) {
					repair(.@checkitem);
					.@checkitem = .@checkitem-1;
				}
				mes "["+ .@npc_name$ +"]";
				mes "Okay! All done. Now, try to be a little more careful. Items have lives too you know.";
				close;
			}
			else {
				mes "["+ .@npc_name$ +"]";
				mes "Mmm? Something's wrong. Wait... Equip the items you need to repair and then come back to me.";
				close;
			}
		case 2:
			mes "["+ .@npc_name$ +"]";
			mes "Well, it's no skin off my nose, but it's not good to leave items damaged. You should get them repaired as soon as possible!";
			close;
		}
	case 2:
		mes "["+ .@npc_name$ +"]";
		mes "Hohoho...";
		mes "You don't have";
		mes "any business with me";
		mes "if you don't have any";
		mes "items to repair.";
		close;
	}
}
