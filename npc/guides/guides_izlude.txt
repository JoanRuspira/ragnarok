//===== rAthena Script ======================================= 
//= Izlude Guides
//============================================================ 

-	script	::GuideIzlude	-1,{
	cutin "prt_soldier",2;
	mes "[Izlude Guide]";
	mes "Welcome to ^8B4513Izlude^000000,";
	mes "Prontera's satellite town.";
	mes "Do you need help navigating the town?";
	F_Navi("[Izlude Guide]");
	next;
	set .@map$, strnpcinfo(4);
	while (1) {
		switch(select("Main Facilities:Remove Marks from Mini-Map:Cancel")) {
		case 1:
			set .@loop,1;
			while (.@loop) {
				switch(select("Cryptura Academy:Marina:International Airship:Kit Shop:Swordsman Guild:Forge:Previous Menu")) {
				case 1:
					mes "[Izlude Guide]";
					mes "Novice adventurers should go to "+F_Navi("Cryptura Academy","iz_ac01,100,39")+" to receive more information and training!";
					mes "Would you like to check any other locations?";
					viewpoint 1,128,260,0,0xD2691E;
					next;
					break;
				case 2:
					mes "[Izlude Guide]";
					mes "You can use the "+F_Navi("Marina",.@map$+",197,205","^B9062F");
					mes "to go to ^8B4513Alberta^000000 or ^8B4513Byalan Island^000000.";
					mes "Beware of ^8B4513Izlude Dungeon^000000";
					mes "on Byalan Island since it is";
					mes "filled with dreadful monsters.";
					mes "Would you like to check any other locations?";
					viewpoint 1,197,205,1,0xFF0000;
					next;
					break;
				case 3:
					mes "[Izlude Guide]";
					mes "The "+F_Navi("International Airship",.@map$+",202,75","^0000FF");
					mes "goes to ^8B4513Yuno^000000 and ^8B4513Rachel^000000.";
					mes "Let me mark its location";
					mes "on your mini-map.";
					mes "Would you like to check any other locations?";
					viewpoint 1,210,73,2,0x0A82FF;
					next;
					break;
				case 4:
					callsub L_Mark, F_Navi("Kit Shop",.@map$+",110,182","^006400");
					viewpoint 1,108,179,4,0xAAFF00;
					next;
					break;
				case 5:
					mes "[Izlude Guide]";
					mes "The "+F_Navi("Swordsman Guild",.@map$+",51,172")+" is where you can ^800080change your job to Swordsman.^000000";
					mes "Let me mark its location";
					mes "on your mini-map.";
					mes "Would you like to check any other locations?";
					viewpoint 1,52,172,5,0xDA70D6;
					next;
					break;
				case 6:
					callsub L_Mark, F_Navi("Forge",.@map$+",153,126");
					viewpoint 1,153,126,7,0xAAFF00;
					next;
					break;
				case 7:
					set .@loop,0;
					break;
				}
			}
			break;
		case 2:
			mes "[Izlude Guide]";
			mes "Sure, I'll remove all marks from your mini-map.";
			mes "Is there anything else I can do for you?";
			viewpoint 2,1,1,0,0xFFFF00;
			viewpoint 2,1,1,1,0x000000;
			viewpoint 2,1,1,2,0xFF0000;
			viewpoint 2,1,1,3,0xFFFF00;
			viewpoint 2,1,1,4,0xFFFF00;
			viewpoint 2,1,1,5,0xFFFF00;
			viewpoint 2,1,1,6,0xFFFF00;
			viewpoint 2,1,1,7,0xFFFF00;
			viewpoint 2,1,1,8,0xFFFF00;
			viewpoint 2,1,1,9,0xFFFF00;
			viewpoint 2,1,1,10,0xFFFF00;
			viewpoint 2,1,1,11,0xFFFF00;
			viewpoint 2,1,1,12,0xFFFF00;
			viewpoint 2,1,1,13,0xFFFF00;
			next;
			break;
		case 3:
			mes "[Izlude Guide]";
			mes "Enjoy your stay.";
			mes "Bye~!";
			close2;
			cutin "prt_soldier",255;
			end;
		}
	}
	end;
L_Mark:
	mes "[Izlude Guide]";
	if (getarg(1,0)) {
		mes "Let me mark the location of the";
		mes getarg(0);
	} else
		mes "Let me mark the location of the "+getarg(0);
	mes "on your mini-map.";
	mes "Would you like to check any other locations?";
	return;
}

izlude,129,175,0	duplicate(GuideIzlude)	Guide#01izlude	105
izlude_a,129,175,4	duplicate(GuideIzlude)	Guide#01izlude_a	105
izlude_b,129,175,4	duplicate(GuideIzlude)	Guide#01izlude_b	105
izlude_c,129,175,4	duplicate(GuideIzlude)	Guide#01izlude_c	105
izlude_d,129,175,4	duplicate(GuideIzlude)	Guide#01izlude_d	105

izlude,133,113,4	duplicate(GuideIzlude)	Guide#02izlude	105
izlude_a,133,113,4	duplicate(GuideIzlude)	Guide#02izlude_a	105
izlude_b,133,113,4	duplicate(GuideIzlude)	Guide#02izlude_b	105
izlude_c,133,113,4	duplicate(GuideIzlude)	Guide#02izlude_c	105
izlude_d,133,113,4	duplicate(GuideIzlude)	Guide#02izlude_d	105

izlude,120,206,6	duplicate(GuideIzlude)	Guide#03izlude	105
